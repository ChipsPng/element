; Element NSIS Install Script
; Michael Fisher <mfisher@kushview.net>

;--------------------------------
; Includes
!include 'LogicLib.nsh'
!include "MUI2.nsh"
!include x64.nsh

;--------------------------------
; Defines
!define TOPDIR "@SOURCE_ROOT@"
!define BUILD_ROOT "@BUILD_ROOT@"
!define ELEMENT_PLUGINS 1
!define PACKAGE_BASENAME "element"
!define ELEMENT_ARCH "win64"
!define ELEMENT_VERSION "@ELEMENT_VERSION@"
!define ELEMENT_APP_NAME "Element"
!define ELEMENT_EXE_FILE "${BUILD_ROOT}\element.exe"
!define ELEMENT_INSTDIR "$ProgramFiles64\Kushview\Element"

!define ELEMENT_VST_DIR "$ProgramFiles64\VstPlugins"
!define ELEMENT_VST_FILE "${BUILD_ROOT}\stage\lib\vst\KV-Element.dll"
!define ELEMENT_VST_FX_FILE "${BUILD_ROOT}\stage\lib\vst\KV-Element-FX.dll"

!define ELEMENT_VST3_DIR "$ProgramFiles64\Common Files\VST3"
!define ELEMENT_VST3_FILE "${BUILD_ROOT}\stage\lib\vst3\KV-Element.vst3"
!define ELEMENT_VST3_FX_FILE "${BUILD_ROOT}\stage\lib\vst3\KV-Element-FX.vst3"

!define ELEMENT_AAX_DIR "$ProgramFiles64\Common Files\Avid\Audio\Plug-Ins"
!define ELEMENT_AAX_FILE "${TOPDIR}\build\x64\Release\AAX\KV-Element.aaxplugin"
!define ELEMENT_AAX_FX_FILE "${TOPDIR}\build\x64\Release\AAX\KV-Element-FX.aaxplugin"

!define INSTALLER_ICON "${TOPDIR}\tools\windeploy\icon.ico"
!define UNINSTALLER "@UNINSTALLER@"
!define UNINSTALLER_FILE "${BUILD_ROOT}\${UNINSTALLER}"

;--------------------------------
; Branding
!define MUI_PRODUCT "${ELEMENT_APP_NAME}"
!define MUI_FILE "savefile"
!define MUI_VERSION "${ELEMENT_VERSION}"
!define MUI_BRANDINGTEXT "${MUI_PRODUCT} v${MUI_VERSION}"

!ifdef INNER
  OutFile "${BUILD_ROOT}\tempinstaller.exe"
  SetCompress off
!else
  OutFile "@OUTFILE@"
  SetCompressor /SOLID lzma
!endif

;--------------------------------
; Initialization - Setup the Install Directory...
!ifdef ELEMENT_PLUGINS
Var VST2DIR
Var VST3DIR
; Var AAXDIR
!endif

Function .onInit
!ifdef INNER
  ; If INNER is defined, then we aren't supposed to do anything except write out
  ; the installer.  This is better than processing a command line option as it means
  ; this entire code path is not present in the final (real) installer.
  WriteUninstaller "@UNINSTALLER_TO_WRITE@"
  Quit  ; just bail out quickly when running the "inner" installer
!endif
  UserInfo::GetAccountType
  pop $0
  ${If} $0 != "admin" ;Require admin rights on NT4+
    MessageBox mb_iconstop "Administrator rights required!"
    SetErrorLevel 740 ;ERROR_ELEVATION_REQUIRED
    Quit
  ${EndIf}
  StrCpy $InstDir "${ELEMENT_INSTDIR}"

 !ifdef ELEMENT_PLUGINS
  StrCpy $VST2DIR "${ELEMENT_VST_DIR}"
  StrCpy $VST3DIR "${ELEMENT_VST3_DIR}"
  ; StrCpy $AAXDIR "${ELEMENT_AAX_DIR}"
 !endif
FunctionEnd

;--------------------------------
; Setup Branding
CRCCheck On
BrandingText "${MUI_BRANDINGTEXT}"

;--------------------------------
; General
;Name and file
Name "${ELEMENT_APP_NAME}"

;Request application privileges for Windows Vista
RequestExecutionLevel admin

;--------------------------------
;Interface Settings
!define MUI_ABORTWARNING

;--------------------------------
;Icons To Use
!define MUI_ICON ${INSTALLER_ICON}
!define MUI_UNICON ${INSTALLER_ICON}

;--------------------------------
;Pages
; !define MUI_FINISHPAGE_NOAUTOCLOSE
!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_LICENSE "${TOPDIR}\LICENSE"

!define MUI_PAGE_HEADER_SUBTEXT "Standalone Installation Path"
!define MUI_DIRECTORYPAGE_TEXT_TOP "To install the standalone app in a differenct folder, click Browse and select another folder. Click Next to continue."
!insertmacro MUI_PAGE_DIRECTORY

!ifdef ELEMENT_PLUGINS
 !define MUI_PAGE_HEADER_SUBTEXT "VST2 Plugin Installation Path"
 !define MUI_DIRECTORYPAGE_TEXT_TOP "To install the VST2 plugins in a differenct folder, click Browse and select another folder. Click Next to continue."
 !define MUI_DIRECTORYPAGE_VARIABLE $VST2DIR
;  !define MUI_PAGE_CUSTOMFUNCTION_PRE WantVst2Plugin
 !insertmacro MUI_PAGE_DIRECTORY

 !define MUI_PAGE_HEADER_SUBTEXT "VST3 Plugin Installation Path"
 !define MUI_DIRECTORYPAGE_TEXT_TOP "To install the VST3 plugins in a differenct folder, click Browse and select another folder. Click Next to continue."
 !define MUI_DIRECTORYPAGE_VARIABLE $VST3DIR
;  !define MUI_PAGE_CUSTOMFUNCTION_PRE WantVst3Plugin
 !insertmacro MUI_PAGE_DIRECTORY
!endif

!insertmacro MUI_PAGE_COMPONENTS
!insertmacro MUI_PAGE_INSTFILES

!define MUI_FINISHPAGE_RUN "$INSTDIR\${ELEMENT_APP_NAME}.exe"
!define MUI_FINISHPAGE_RUN_TEXT "Launch ${ELEMENT_APP_NAME}"

!define MUI_FINISHPAGE_TEXT_REBOOT "The setup needs to reboot"
!define MUI_FINISHPAGE_TEXT_REBOOTNOW "Reboot now"
!define MUI_FINISHPAGE_TEXT_REBOOTLATER "Reboot later"

!insertmacro MUI_PAGE_FINISH

!ifdef INNER
 !insertmacro MUI_UNPAGE_CONFIRM
 !insertmacro MUI_UNPAGE_INSTFILES
!endif

;--------------------------------
;Languages
!insertmacro MUI_LANGUAGE "English"

;--------------------------------
; MUI System
; !insertmacro MUI_SYSTEM

;--------------------------------
; Installer Sections
Section "${ELEMENT_APP_NAME} Application" SecApp
    ; Read Only (mandatory)
    SectionIn RO

    SetOutPath "$INSTDIR"
    File "${ELEMENT_EXE_FILE}"

    SetOutPath "$INSTDIR\scripts"
    File /r "${TOPDIR}\scripts\*.lua"

    ; Change back to root install dir
    SetOutPath "$INSTDIR"
!ifndef INNER
    ; this packages the signed uninstaller
    File "${UNINSTALLER_FILE}"
!endif
    SetRegView 64
    WriteRegStr HKLM "Software\Kushview\Element" "InstallDir" "$\"$INSTDIR$\""
    WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${ELEMENT_APP_NAME}" \
                     "DisplayName" "${ELEMENT_APP_NAME}"
    WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${ELEMENT_APP_NAME}" \
                     "Publisher" "Kushview, LLC"
    WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${ELEMENT_APP_NAME}" \
                     "UninstallString" "$\"$INSTDIR\${UNINSTALLER}$\""
SectionEnd

!ifndef INNER
!ifdef ELEMENT_PLUGINS
Section "VST Plugin" SecVST
    SetOutPath $VST2DIR
    File "${ELEMENT_VST_FILE}"
    File "${ELEMENT_VST_FX_FILE}"
SectionEnd

Section "VST3 Plugin" SecVST3
    SetOutPath "$VST3DIR\KV-Element.vst3"
    File /r "${ELEMENT_VST3_FILE}\*.vst3"
    SetOutPath "$VST3DIR\KV-Element-FX.vst3"
    File /r "${ELEMENT_VST3_FX_FILE}\*.vst3"
SectionEnd

; Section "AAX Plugin" SecAAX
;     SetOutPath "${ELEMENT_AAX_DIR}\KV-Element.aaxplugin"
;     File /r "${ELEMENT_AAX_FILE}\*.*"
;     File /r "${ELEMENT_AAX_FX_FILE}\*.*"
; SectionEnd
!endif
!endif

;--------------------------------
; Desktp Shortcut Section
Section "Desktop Shortcut" secDesktop
    SetShellVarContext current
    CreateShortCut "$DESKTOP\${ELEMENT_APP_NAME}.lnk" "$INSTDIR\${ELEMENT_APP_NAME}.exe"
SectionEnd

;--------------------------------
; Start Menu Section
Section "Start Menu" secStartMenu
    SetShellVarContext current
    CreateDirectory "$SMPROGRAMS\Kushview\Element"
    CreateShortCut "$SMPROGRAMS\Kushview\Element\${ELEMENT_APP_NAME}.lnk" "$INSTDIR\${ELEMENT_APP_NAME}.exe"
    CreateShortCut "$SMPROGRAMS\Kushview\Element\Uninstall ${ELEMENT_APP_NAME}.lnk" "$INSTDIR\${UNINSTALLER}"
SectionEnd

!ifdef INNER
;--------------------------------
; Uninstaller Section
Section "Uninstall"
    SetShellVarContext current
    Delete "$DESKTOP\${ELEMENT_APP_NAME}.lnk"
    Delete "$INSTDIR\${UNINSTALLER}"
    Delete "$INSTDIR\${ELEMENT_APP_NAME}.exe"
    RmDir /r "$INSTDIR\Scripts"
    RmDir /r "$INSTDIR\Modules"

   !ifdef ELEMENT_PLUGINS
    Delete "${ELEMENT_VST_DIR}\KV-Element.*"
    RmDir /r "${ELEMENT_VST3_DIR}\KV-Element.vst3"
    RmDir /r "${ELEMENT_AAX_DIR}\KV-Element.aaxplugin"

    Delete "${ELEMENT_VST_DIR}\KV-Element-FX.*"
    RmDir /r "${ELEMENT_VST3_DIR}\KV-Element-FX.vst3"
    RmDir /r "${ELEMENT_AAX_DIR}\KV-Element-FX.aaxplugin"
   !endif
   
    RmDir /r "$SMPROGRAMS\Kushview\Element"
    SetRegView 64
    DeleteRegKey HKLM "Software\Kushview\Element"
    DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\${ELEMENT_APP_NAME}"
    DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Run\${ELEMENT_APP_NAME}"
SectionEnd
!endif
